#pragma kernel CSMain

struct Triangle
{
    float3 v0;
    float3 v1;
    float3 v2;
    float3 normal;
    float area;
};

struct GrassBlade
{
    float3 position;
    float3 normal;
    float height;
    float rotation;
};

StructuredBuffer<Triangle> _Triangles;
AppendStructuredBuffer<GrassBlade> _Blades;

int _TriangleCount;
int _BladesPerSquareUnit;
float _BladeHeight;

[numthreads(64, 1, 1)]
void CSMain(uint id : SV_DispatchThreadID)
{
    if (id >= _TriangleCount) return;

    Triangle tri = _Triangles[id];

    uint bladeCount = tri.area * _BladesPerSquareUnit;
    for (int i = 0; i < bladeCount; i++)
    {
        float2 r = float2(frac(sin(dot(float2(id, i), float2(12.9898,78.233))) * 43758.5453),
        frac(sin(dot(float2(i, id), float2(39.3467,11.135))) * 96321.517));

        if (r.x + r.y > 1.0)
            r = 1.0 - r;

        float3 pos =
            tri.v0 +
            r.x * (tri.v1 - tri.v0) +
            r.y * (tri.v2 - tri.v0);

        GrassBlade blade;
        blade.position = pos;
        blade.normal = tri.normal;
        blade.height = _BladeHeight;

        float rand = frac(sin(dot(float2(id, i), float2(91.345, 12.678))) * 47453.5453);
        blade.rotation = rand * 6.28318530718; // 2Ï€

        _Blades.Append(blade);
    }
}
